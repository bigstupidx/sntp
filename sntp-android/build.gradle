apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile project(':sntp')
    compile 'com.android.support:support-annotations:22.2.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

publishing {
    publications {
        sntpAndroid(MavenPublication) {
            pom.withXml {
                def dep = asNode().appendNode('dependencies').appendNode('dependency')
                dep.appendNode('groupId', project(':sntp').group)
                dep.appendNode('artifactId', project(':sntp').name)
                dep.appendNode('version', project(':sntp').version)
                dep.appendNode('scope', 'compile')
            }
            artifact bundleRelease
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            url new File(rootProject.rootDir, 'm2/repository')
        }
    }
}
